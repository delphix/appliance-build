/*
 * Copyright 2019 Delphix
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "base"

apply from: "${rootProject.projectDir}/gradle-lib/util.gradle"

task ancillaryRepository(type: Exec) {
    inputs.file "${rootProject.projectDir}/scripts/build-ancillary-repository.sh"

    for (envVar in ["AWS_S3_URI_VIRTUALIZATION",
                    "AWS_S3_URI_USERLAND_PKGS",
                    "AWS_S3_URI_MASKING",
                    "AWS_S3_URI_ZFS",
                    "AWS_S3_PREFIX_VIRTUALIZATION",
                    "AWS_S3_PREFIX_MASKING",
                    "AWS_S3_PREFIX_USERLAND_PKGS",
                    "AWS_S3_PREFIX_KERNEL_PKGS"]) {
        inputs.property(envVar, System.getenv(envVar)).optional(true)
    }

    outputs.files fileTree("${buildDir}/ancillary-repository") {
        // Files that are modified when we serve from this apt repo
        exclude "db/"
    }

    commandLine "${rootProject.projectDir}/scripts/build-ancillary-repository.sh"
}

createArtifactsDirTask(this)

def artifactTypes = ["aws": "vmdk",
                     "azure": "vhdx",
                     "esx": "ova",
                     "gcp": "gcp.tar.gz",
                     "kvm": "qcow2"]

def configTaskSuffix = "Configuration"
def configRunType = "configuration"
def upgradeArtifactsTaskSuffix = "UpgradeArtifacts"
def upgradeArtifactsRunType = "upgrade-artifacts"
def vmArtifactsTaskSuffix = "VmArtifacts"
def vmArtifactsRunType = "vm-artifacts"

/*
 * Create tasks for common configuration hooks.
 */
for (variant in allVariants) {
    for (platform in allPlatforms) {
        def taskSuffix = "${toCamelCase(variant).capitalize()}${platform.capitalize()}"
        def t = tasks.create("build${taskSuffix}${configTaskSuffix}", Exec) { task ->
            group = 'Build'
            description = "Runs the common configuration hooks for the live build"
            dependsOn ancillaryRepository
            dependsOn mkArtifactsDir

            inputs.dir "${projectDir}/auto"
            inputs.files fileTree("${projectDir}/config") {
                exclude "hooks/"
            }
            inputs.dir "${projectDir}/config/hooks/${configRunType}"
            inputs.dir "${projectDir}/misc"
            inputs.dir "${projectDir}/variants"
            inputs.file "${rootProject.projectDir}/scripts/run-live-build.sh"

            for (envVar in ["APPLIANCE_PASSWORD",
                            "DELPHIX_SIGNATURE_URL",
                            "DELPHIX_SIGNATURE_TOKEN",
                            "DELPHIX_APPLIANCE_VERSION"]) {
                inputs.property(envVar, System.getenv(envVar)).optional(true)
            }

            commandLine "${rootProject.projectDir}/scripts/run-live-build.sh", variant, platform, configRunType
        }

        tasks.create("clean${taskSuffix}${configTaskSuffix}", Exec) {
            commandLine "rm", "-rf", "${buildDir}/${variant}-${platform}"
        }
        tasks.clean.dependsOn "clean${taskSuffix}${configTaskSuffix}"
    }
}

/*
 * Create tasks to build upgrade image artifacts. For instance, the task 'buildInternalDevEsxUpgradeArtifacts' will produce
 * an artifact 'internal-dev-esx.debs.tar.gz'. These tasks depend on their configuration task counterpart.
 */
for (variant in allVariants) {
    for (platform in allPlatforms) {
        def taskSuffix = "${toCamelCase(variant).capitalize()}${platform.capitalize()}"
        def t = tasks.create("build${taskSuffix}${upgradeArtifactsTaskSuffix}", Exec) { task ->
            group = 'Build'
            description = "Builds the upgrade image artifacts for the ${variant} variant of the appliance for ${platform}"
            dependsOn "build${taskSuffix}${configTaskSuffix}"

            inputs.dir "${projectDir}/auto"
            inputs.files fileTree("${projectDir}/config") {
                exclude "hooks/"
            }
            inputs.dir "${projectDir}/config/hooks/${upgradeArtifactsRunType}"
            inputs.dir "${projectDir}/misc"
            inputs.dir "${projectDir}/variants"
            inputs.file "${rootProject.projectDir}/scripts/run-live-build.sh"

            for (envVar in ["APPLIANCE_PASSWORD",
                            "DELPHIX_SIGNATURE_URL",
                            "DELPHIX_SIGNATURE_TOKEN",
                            "DELPHIX_APPLIANCE_VERSION"]) {
                inputs.property(envVar, System.getenv(envVar)).optional(true)
            }

            outputs.file "${buildDir}/artifacts/${variant}-${platform}.debs.tar.gz"

            commandLine "${rootProject.projectDir}/scripts/run-live-build.sh", variant, platform, upgradeArtifactsRunType
        }

        tasks.create("clean${taskSuffix}${upgradeArtifactsTaskSuffix}", Exec) {
            commandLine "rm", "-rf", "${buildDir}/${variant}-${platform}"
        }
        tasks.clean.dependsOn "clean${taskSuffix}${upgradeArtifactsTaskSuffix}"
    }
}

/*
 * Create tasks to build VM artifacts. For instance, the task 'buildInternalDevEsxVmArtifacts' will produce
 * an artifact 'internal-dev-esx.ova'. These tasks depend on their configuration task counterpart.
 */
for (variant in allVariants) {
    for (platform in allPlatforms) {
        def taskSuffix = "${toCamelCase(variant).capitalize()}${platform.capitalize()}"
        def t = tasks.create("build${taskSuffix}${vmArtifactsTaskSuffix}", Exec) { task ->
            group = 'Build'
            description = "Builds the VM artifacts for the ${variant} variant of the appliance for ${platform}"
            dependsOn "build${taskSuffix}${configTaskSuffix}"

            inputs.dir "${projectDir}/auto"
            inputs.files fileTree("${projectDir}/config") {
                exclude "hooks/"
            }
            inputs.dir "${projectDir}/config/hooks/${vmArtifactsRunType}"
            inputs.dir "${projectDir}/misc"
            inputs.dir "${projectDir}/variants"
            inputs.file "${rootProject.projectDir}/scripts/run-live-build.sh"

            for (envVar in ["APPLIANCE_PASSWORD",
                            "DELPHIX_SIGNATURE_URL",
                            "DELPHIX_SIGNATURE_TOKEN",
                            "DELPHIX_APPLIANCE_VERSION"]) {
                inputs.property(envVar, System.getenv(envVar)).optional(true)
            }

            if (variant != "internal-minimal") {
                // We don't bother creating a migration image for internal-minimal
                outputs.file "${buildDir}/artifacts/${variant}-${platform}.migration.tar.gz"
            }
            outputs.file "${buildDir}/artifacts/${variant}-${platform}.${artifactTypes[platform]}"

            commandLine "${rootProject.projectDir}/scripts/run-live-build.sh", variant, platform, vmArtifactsRunType
        }

        tasks.create("clean${taskSuffix}${vmArtifactsTaskSuffix}", Exec) {
            commandLine "rm", "-rf", "${buildDir}/${variant}-${platform}"
        }
        tasks.clean.dependsOn "clean${taskSuffix}${vmArtifactsTaskSuffix}"
    }
}

/*
 * Create tasks to build everything. These tasks simply run both upgrade and VM image tasks.
 */
for (variant in allVariants) {
    for (platform in allPlatforms) {
        def taskSuffix = "${toCamelCase(variant).capitalize()}${platform.capitalize()}"
        def t = tasks.create("build${taskSuffix}", Exec) { task ->
            group = 'Build'
            description = "Builds all artifacts for the ${variant} variant of the appliance for ${platform}"
            dependsOn "build${taskSuffix}${upgradeArtifactsTaskSuffix}"
            dependsOn "build${taskSuffix}${vmArtifactsTaskSuffix}"
        }

        tasks.create("clean${taskSuffix}", Exec) {
            commandLine "rm", "-rf", "${buildDir}/${variant}-${platform}"
        }
        tasks.clean.dependsOn "clean${taskSuffix}"
    }
}

/*
 * Fetch pre-built vm artifacts instead of building them. See fetch-livebuild-artifacts.sh for
 * details.
 */
task fetchLiveBuildArtifacts(type: Exec) {
    dependsOn mkArtifactsDir
    outputs.dir "${buildDir}/artifacts"
    inputs.property("AWS_S3_URI_LIVEBUILD_ARTIFACTS",
                    System.getenv("AWS_S3_URI_LIVEBUILD_ARTIFACTS"))
    commandLine "${rootProject.projectDir}/scripts/fetch-livebuild-artifacts.sh"
}

task cleanRpool(type: Exec) {
    commandLine "bash", "-c", """
	if zpool list rpool &>/dev/null; then \
		zpool destroy -f rpool; \
	fi
        """
}
tasks.clean.dependsOn "cleanRpool"
